plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'jacoco'
	id 'pmd'
	id "com.diffplug.spotless" version "6.12.0"
	id "com.github.spotbugs" version "5.0.14"
}

group = 'musala.soft'
version = '0.0.1'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	gradlePluginPortal()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('build') {
	dependsOn 'spotlessApply'
}

spotless {
	java {
		googleJavaFormat()
		removeUnusedImports()
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

pmd {
	toolVersion = "6.21.0"
	rulesMinimumPriority = 5
}

spotbugs {
	showStackTraces = false
	showProgress = true
	excludeFilter = file("config/spotbugs/exclude-filter.xml")
}

spotbugsMain {
	reports {
		html {
			required = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

spotbugsTest {
	reports {
		html {
			required = true
			stylesheet = 'fancy-hist.xsl'
		}
	}
}